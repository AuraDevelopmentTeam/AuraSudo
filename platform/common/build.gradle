apply from: "../../GradleCommon/common.gradle"

import org.apache.tools.ant.filters.ReplaceTokens

configurations {
    slf4j {
        transitive = false
    }
}

repositories {
    maven {
        name "sponge"
        url "https://repo.spongepowered.org/maven"
    }
}

dependencies {
    api project(":AuraSudo-api")

    shadow(project(":AuraSudo-api")) {
        transitive = false
    }

    // Normal dependencies
    api "org.slf4j:slf4j-api:${slf4j_version}"
    api "com.github.ben-manes.caffeine:caffeine:${caffeine_version}"
    api "org.spongepowered:configurate-hocon:${configurate_version}"
    api "net.luckperms:api:${luckPerms_version}"

    // Shadow dependencies
    shadow("team.aura_dev.lib.multiplatformcore:MultiPlatformCore:${multiPlatformCore_version}") {
        transitive = false
    }

    // Test dependencies
    testRuntimeOnly "org.slf4j:slf4j-nop:${slf4j_version}"

    // SLF4J files
    slf4j "org.slf4j:slf4j-api:${slf4j_version}"
}

processSources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "slf4jVersion", slf4j_version
    inputs.property "slf4jPluginVersion", slf4jPlugin_version

    filter (ReplaceTokens, tokens: [
        "slf4jVersion": slf4j_version,
        "slf4jPluginVersion": slf4jPlugin_version
    ])
}

shadowJar {
    relocate("${rootProject.baseGroup}.lib", "${rootProject.group}.lib")
    relocate("licenses", "${rootProject.group}.shadow.licenses")

    // For picomaven
    exclude "module-info.class"

    from(project.configurations.slf4j.files) {
        into "org/slf4j"
        rename '(.*).jar', '$1.zip'
    }
}
